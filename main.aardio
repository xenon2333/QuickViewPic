import config;
import fonts.fontAwesome;
import fsys;
import gdip.bitmap;
import process;
import win.clip.viewer;
import win.timer;
import win.ui;
import win.ui.menu;
import win.ui.tracker;
import winex.tooltip;
win.ui.disableDpiScaling = true;//禁用DPI自动缩放，避免显示异常

//声明全局常量和变量
_version = "0.2.0";
clip = {};
/*DSG{{*/
var winlist = win.form(text="列表";right=99;bottom=39;bgcolor=3618615;border="none";exmode="toolwindow";max=false;min=false;mode="popup")
winlist.add(
button_menu={cls="plus";text="QuickViewPic";left=0;top=0;right=100;bottom=20;bgcolor=-13951306;color=16777215;font=LOGFONT(h=-14);z=1};
button_pgdown={cls="plus";left=80;top=20;right=100;bottom=40;iconStyle={font=LOGFONT(h=-14;name='FontAwesome');valign="bottom"};iconText='\uF054';z=4};
button_pgup={cls="plus";left=0;top=20;right=20;bottom=40;iconStyle={font=LOGFONT(h=-14;name='FontAwesome');valign="bottom"};iconText='\uF053';z=3};
listbox={cls="listbox";left=0;top=40;right=100;bottom=120;bgcolor=3618615;color=16777215;font=LOGFONT(h=-14);integralHeight=0;items={"1";"2";"3"};z=2};
static={cls="static";text="第1页";left=20;top=20;right=80;bottom=40;align="center";center=1;color=16777215;font=LOGFONT(h=-14);transparent=1;z=5}
)
/*}}*/

var value = {};//图片地址数组
var page, maxpage;//页码

/*监视鼠标是否在窗口内{{*/
var mtimer = win.timer(winlist);
mtimer.onTimer = function(){
	var msPos = ::POINT();
	::User32.GetCursorPos( msPos );
	var hwnd = ::User32.WindowFromPoint( msPos.x, msPos.y );
	select( hwnd ){
		case winlist.hwnd,winlist.button_menu.hwnd,winlist.button_pgup.hwnd,winlist.button_pgdown.hwnd,winlist.listbox.hwnd
			movewin('Show');
		else
			movewin('Hide');
	}
}
/*}}*/

/*刷新翻页状态{{*/
var ptimer = win.timer(winlist);
ptimer.onTimer = function(){
	maxpage = math.ceil( table.count( value )/config.Perpage );
	if( page == 1 )
		winlist.button_pgup.disabled = 1;
	else
		winlist.button_pgup.disabled = 0;
	if( maxpage == 0 || page == maxpage )
		winlist.button_pgdown.disabled = 1;
	else
		winlist.button_pgdown.disabled = 0;
	if( winlist.static.text != "第" ++ page ++"/" ++ maxpage ++ "页" )
		winlist.static.text = "第" ++ page ++"/" ++ maxpage ++ "页";
}
/*}}*/

/*创建菜单{{*/
var menu = win.ui.popmenu( winlist );
menu.addTable( {
	{ "QuickViewPic"; };
	{ /*分隔线*/ };
	{ "全局设置"; function(id){
		winlist.show(false);
		if( winlist.loadForm("\dlg\global_setting.aardio").doModal() ){
			initialize();
			movewin('Hide');
			var scrw, scrh = win.getScreen();
			if( config.Dockside == 'Right' )
				winex.tooltip.trackPopup('我藏在这里了哦！',scrw,winlist.bottom);
			else
				winex.tooltip.trackPopup('我藏在这里了哦！',1,winlist.bottom);
		}
		winlist.show();
 	} };
	{ "高级设置"; function(id){
		winlist.show(false);
		select( config.Mode ){
			case 'Default'
				winlist.msgbox("默认模式下无高级设置。","信息");
			case 'CustomDir'{
				if( winlist.loadForm("\dlg\customdir_setting.aardio").doModal() )
					loaddata();
			}
			case 'CustomList'{
				if( winlist.loadForm("\dlg\customlist_setting.aardio").doModal() )
					loaddata();
			}
			case 'Clip'{
				winlist.loadForm("\dlg\clip_setting.aardio").doModal();
				loaddata();
			}
		}
		winlist.show();
	} };
 	{ /*分隔线*/ };
	{ "主页"; function(id){
		process.openUrl("https://github.com/xenon2333/QuickViewPic");
 	} };
 	{ "关于"; function(id){
		var str = "QuickViewPic v"++_version++'\r\nWritten by Xenon\r\nEmail：xenon2333@qq.com';
		winlist.show(false);
		winlist.msgbox(str,"关于");
		winlist.show();
 	} };
	{ "退出"; function(id){  
		winlist.close();
 	} };
} )
menu.enable(1,false);
/*}}*/

/*移动窗口{{*/
movewin = function( v ){
	var scrw, scrh = win.getScreen();
	var x, y, z;
	y = scrh/2 - (40+config.Perpage*19)/2;
	if( y < 0 )
		y = 0;
	select( config.Dockside ) {
		case 'Left' x, z = 0, -97;
		case 'Right' x, z = scrw-100, 97;
		else x, z = 0, -97;
	}
	select( v ) {
		case 'Show'{
			winlist.setPos(x,y);
			winlist.listbox.show();
		}
		case 'Hide'{
			winlist.listbox.show(false);
			winlist.setPos(x+z,y);
		}
	}
}
/*}}*/

/*菜单按钮{{*/
winlist.button_menu.onMouseEnter = function(wParam,lParam){
	winlist.button_menu.text = "Menu";
}
winlist.button_menu.onMouseLeave = function(wParam,lParam){
	winlist.button_menu.text = "QuickViewPic";
}
winlist.button_menu.onMouseClick = function(wParam,lParam){
    menu.popup();
}
/*}}*/

/*翻页按钮{{*/
winlist.button_pgup.onMouseClick = function(wParam,lParam){
    if( page > 1 ){
    	page--;
    	refresh();
    }
}
winlist.button_pgdown.onMouseClick = function(wParam,lParam){
    if( page < maxpage ){
    	page++;
    	refresh();
    }
}
/*}}*/

/*加载数据{{*/
loaddata = function(){
	page = 1;
	value = {};
	select( config.Mode ){
		case 'Default'{
			var data = fsys.list( io._exedir,,{"*.jpg";"*.png";"*.bmp"});
			var n = table.count( data )/2;
			if( n == 0 )
				winlist.listbox.items = {"当前目录无图片";"请检查设置"};
			else{
				for(i=1;n;1){
					value[i] = {};
					value[i][1] = data[i];
					value[i][2] = data[data[i]];
				}
			}
		}
		case 'CustomDir'{
			if( !io.exist(config.CustomDir.current) ){
				winlist.msgbox("当前路径不存在，程序已自动调整到默认目录。","提示");
				config.CustomDir.current = io._exedir;
			}
			var data = fsys.list( config.CustomDir.current,,{"*.jpg";"*.png";"*.bmp"});
			var n = table.count( data )/2;
			if( n == 0 )
				winlist.listbox.items = {"当前目录无图片";"请检查设置"};
			else{
				for(i=1;n;1){
					value[i] = {};
					value[i][1] = data[i];
					value[i][2] = data[data[i]];
				}
			}
		}
		case 'CustomList'{
			var n = table.count( config.CustomList );
			if( n == 0 )
				winlist.listbox.items = {"未配置图片列表";"请检查设置"};
			else {
				for(i=1;n;1){
					value[i] = {};
					value[i][1] = io.splitpath( config.CustomList[i] ).file;
					value[i][2] = config.CustomList[i];
				}
			}
		}
		case 'Clip'{
			var n = table.count( ..clip );
			if( n == 0 )
				winlist.listbox.items = {"请进入高级设置";"手动配置剪贴板";};
			else {
				for(i=1;n;1){
					value[..clip[i][1]] = {};
					value[..clip[i][1]][1] = ..clip[i][3];
					value[..clip[i][1]][2] = ..clip[i][2];
				}
			}
		}
	}
	refresh();
}
/*}}*/

/*刷新列表{{*/
refresh = function(){
	var n = table.count(value);
	if( n != 0 ){
		var items = {};
		for(i=1;config.Perpage;1){
			var j = (page-1)*config.Perpage+i;
			if( value[j] == null )
				break;
			items[i] = value[j][1];
		}
		winlist.listbox.items = items;
	}
	winlist.listbox.height = 19*table.count(winlist.listbox.items);
}
/*}}*/

/*窗口初始化{{*/
initialize = function(){
	if( config.Theme == 'Light' ){
		winlist.bgcolor = 16119285;
		winlist.static.color = 0;
		winlist.button_menu.skin({
			background={
				default=0xFFF5F5F5;
				hover=0xFFF5F5F5
			};
			color={
				default=0xFFB61E2B;
			}
		})
		winlist.button_pgup.skin({
			background={
				default=0xFFF5F5F5
			};
			color={
				active=0xFFFF0000;
				default=0xFF000000;
				disabled=0xFFF5F5F5;
				hover=0xFFB61E2B
			}
		});
		winlist.button_pgdown.skin({
			background={
				default=0xFFF5F5F5
			};
			color={
				active=0xFFFF0000;
				default=0xFF000000;
				disabled=0xFFF5F5F5;
				hover=0xFFB61E2B
			}
		});
		winlist.listbox.bgcolor = 16119285;
		winlist.listbox.color = 0;
	}
	else {
		winlist.bgcolor = 3618615;
		winlist.static.color = 16777215;
		winlist.button_menu.skin({
			background={
				default=0xFFB61E2B;
				hover=0xFFB61E2B
			};
			color={
				default=0xFFFFFFFF;
			}
		})
		winlist.button_pgup.skin({
			background={
				default=0xFF373737
			};
			color={
				active=0xFFFF0000;
				default=0xFFC4C4C4;
				disabled=0xFF373737;
				hover=0xFFB61E2B
			}
		});
		winlist.button_pgdown.skin({
			background={
				default=0xFF373737
			};
			color={
				active=0xFFFF0000;
				default=0xFFC4C4C4;
				disabled=0xFF373737;
				hover=0xFFB61E2B
			}
		});
		winlist.listbox.bgcolor = 3618615;
		winlist.listbox.color = 16777215;
	}
	winlist.button_pgup.disabled = 0;
	winlist.button_pgdown.disabled = 0;
	winlist.redrawBackground();
	loaddata();
}
/*}}*/

/*监视剪贴板{{*/
win.clip.viewer( winlist );
winlist.onDrawClipboard = function(){
	if( config.Mode == 'Clip' && config.Clip.monitor == 'ON' ){
		var hbmp = win.clip.readBitmap();
		if( hbmp == null )
			return;
		var n = table.count( ..clip ) +1;
		..clip[n] = {};
		..clip[n][1] = n;
		..clip[n][2] = hbmp;
		..clip[n][3] = tostring( time.tick() );
		loaddata();
		publish("ClipChange",);
		publish("AutoSave",);
	}
}
/*}}*/

/*列表框鼠标追踪{{*/
var track_listbox = win.ui.tracker(winlist.listbox);
track_listbox.onMouseMove = function(wParam,lParam){
	var x,y = win.getMessagePos();
	var id = winlist.listbox.hitTest(x,y,true);
	winlist.listbox.selIndex = id;
	id = value[(page-1)*config.Perpage+id][[2]];
	publish("ShowPic", id);
}
track_listbox.onMouseLeave = function(wParam,lParam){
	winlist.listbox.selIndex = 0;
	publish("Hide",);
	win.setTopmost(winlist.hwnd);//窗口置顶
}
track_listbox.onRightMouseUp = function(wParam,lParam){
    var x,y = win.getMessagePos();
	var n = winlist.listbox.hitTest(x,y,true);
	if( !n ) return;
	n = (page-1)*config.Perpage+n;
	if( !value[n][[2]] ) return;
	if( config.Mode == 'Clip' ){
		if( config.Clip.monitor == 'ON' ){
			config.Clip.monitor = 'OFF';//临时禁用监视
			win.clip.writeBitmap( value[n][2], false );
			config.Clip.monitor = 'ON';
		}
		else
			win.clip.writeBitmap( value[n][2], false );
	}
	else {
		var bmp = gdip.bitmap( value[n][2] );
		var hbmp = bmp.copyHandle();
		win.clip.writeBitmap( hbmp, false );
		::DeleteObject(hbmp);
		bmp.dispose();
	}
}
/*}}*/

/*订阅消息{{*/
subscribe("AutoSave",function(...){
	var n = table.count(..clip);
	if( config.Clip.autosave == 'ON' ){
		if( !io.exist( config.Clip.savepath ) )
 			io.createDir( config.Clip.savepath );
 		var path = config.Clip.savepath ++ "\" ++ tostring( time(), config.Clip.filename ) ++ config.Clip.format;
 		var bmp = gdip.bitmap( ..clip[n][2] );
 		bmp.save( path );
 		bmp.dispose();
	}
} )
/*}}*/

/*初始化程序{{*/
winlist.listbox.orphanWindow();
winlist.loadForm("\dlg\pic.aardio");
initialize();
movewin('Show');
ptimer.onTimer();
ptimer.enable(50,);
winlist.show();
win.setTopmost(winlist.hwnd);//窗口置顶
win.delay(1000);//首次启动先显示1秒再贴边隐藏
mtimer.enable(100,);
var scrw, scrh = win.getScreen();
if( config.Dockside == 'Right' )
	winex.tooltip.trackPopup('我藏在这里了哦！',scrw,winlist.bottom);
else
	winex.tooltip.trackPopup('我藏在这里了哦！',1,winlist.bottom);
/*}}*/
win.loopMessage();
