import config;
import win.ui;
/*DSG{{*/
var winpic = win.form(text="Pic";right=19;bottom=19;border="none";exmode="toolwindow";max=false;min=false;mode="popup";title=false)
winpic.add(
plus={cls="plus";left=0;top=0;right=20;bottom=20;foreRepeat="scale";z=1}
)
/*}}*/

if( !winpic.parent ){
	return; //阻止该窗口单独启动
}
import gdip.bitmap;

var history, pic;

/*调整图片框大小及窗口位置{{*/
adapt = function( w, h ){
	var scrw, scrh = win.getScreen();
	var dx, dy;
	if( h > scrh : w > scrw ){
		if( h/w > scrh/(scrw-config.Width-20) ){
			winpic.plus.height = scrh;
			winpic.plus.width = math.floor(w*scrh/h);
			dx, dy = winpic.plus.width, 0;
		}
		else {
			winpic.plus.height = math.floor(h*(scrw-config.Width-20)/w);
			winpic.plus.width = scrw-config.Width-20;
			dx, dy = winpic.plus.width, scrh/2-math.floor(winpic.plus.height/2);
		}
	}
	else {
		winpic.plus.width = w;
		winpic.plus.height = h;
		dx, dy = winpic.plus.width, scrh/2-math.floor(h/2);
	}
	select( config.Dockside ) {
		case 'Left' winpic.setPos( config.Width + 20, dy );
		case 'Right' winpic.setPos( scrw - config.Width - 20 - dx, dy );
		else winpic.setPos( config.Width + 20, dy );
	}
}
/*}}*/

/*设置图片{{*/
setpic = function( v ){
	if( pic )
		pic.dispose();
	pic = gdip.bitmap( v );
	if( pic == null ){
		win.msgboxErr('读取图片失败！\r\n请检查文件路径是否正确、格式是否为支持的格式！',"错误");
		return;
	}
	winpic.plus.setForeground(0x00000000);
	adapt( pic.width, pic.height );
	winpic.plus.setForeground(pic,false);
}
/*}}*/

subscribe("ShowPic",function(...){
	var para = {...};
	var value = para[1];
	if( !value )
		winpic.plus.setForeground(0x00000000);
	else if( value != history){
		history = value;
		setpic( value );
	}
} )

subscribe("Hide",function(...){
	history = null;
	if( pic )
		pic.dispose();
	winpic.plus.setForeground(0x00000000);
} )

winpic.plus.orphanWindow(true,);
win.loopMessage();
return winpic;
