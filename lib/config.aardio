//config 配置
import fsys;
import fsys.path;
import fsys.table;

config = fsys.table(io.appData("/QuickViewPic/config.cfg"));
//删除无关量
for(k,v in config){
	select(k){
		case '_gc','X','Y','Mode','Perpage','Theme','Width','Oneclickcopy','Wheelturn','CustomDir','CustomList','Clip'{}
		else
			config[k] = null;
	}
}
//窗口位置
if( !math.isInteger(config.X) )
	config.X = null;
if( !math.isInteger(config.Y) )
	config.Y = null;
//全局设置
select( config.Mode ){
	case 'Default','CustomDir','CustomList','Clip' {}
	else
		config.Mode = 'Default';
}
select( config.Perpage ) {
	case 20,25,50,100 {}
	else
		config.Perpage = 20;
}
select( config.Theme ){
	case 'Light','Dark'{}
	else
		config.Theme = 'Dark';
}
if( config.Width != null ){
	if( !math.isInteger( config.Width ) )
		config.Width = math.ceil( math.abs( config.Width ) );
	else
		config.Width = math.abs( config.Width );
	if( config.Width > 300 )
		config.Width = 300;
	if( config.Width < 100 )
		config.Width = 100;
}
else
	config.Width = 100;
if( config.Oneclickcopy != 'ON' )
	config.Oneclickcopy = 'OFF';
if( config.Wheelturn != 'ON' )
	config.Wheelturn = 'OFF';

//自定义目录设置
if( !table.count(config.CustomDir) ){
	config.CustomDir = {
		current = io._exedir;
		root = io._exedir;
		blacklist = {};
	};
}
if( !fsys.isDir(config.CustomDir.root) ){
	config.CustomDir.root = io._exedir;
	config.CustomDir.current = io._exedir;
}
if( !fsys.isDir(config.CustomDir.current) )
	config.CustomDir.current = config.CustomDir.root;
if( !table.count(config.CustomDir.blacklist) )
	config.CustomDir.blacklist = {};
var n = table.count(config.CustomDir.blacklist);
var tab = config.CustomDir.blacklist;
for(i=1;n;1){
	if( !fsys.isDir(config.CustomDir.blacklist[i]) )
		table.remove(tab,i);
}
config.CustomDir.blacklist = tab;

//自定义列表设置
if( !table.count(config.CustomList) ){
	config.CustomList = {};
}
tab = config.CustomList;
n = table.count(config.CustomList);
for(i=1;n;1){
	if( !io.exist(config.CustomList[i]) )
		table.remove(tab,i);
}
config.CustomList = tab;

//剪贴板设置
if( table.count(config.Clip) ){
	if( config.Clip.monitor != 'ON' )
		config.Clip.monitor = 'OFF';
	if( config.Clip.autosave != 'ON' )
		config.Clip.autosave = 'OFF';
	config.Clip.savepath = fsys.path.removeBackslash( config.Clip.savepath );
	try{ tostring( time(), config.Clip.filename );}
	catch(){ config.Clip.filename = '%y%m%d_%H%M%S'; }
	if( config.Clip.filename != fsys.path.validName(config.Clip.filename) )
		config.Clip.filename = '%y%m%d_%H%M%S';
	select( config.Clip.format ) {
		case '.jpg','.png','.bmp' {}
		else
			config.Clip.format = '.png';
	}
}
else
	config.Clip = {
		monitor = 'OFF';
		autosave = 'OFF';
		savepath = io.appData("\QuickViewPic\autosave");
		filename = '%y%m%d_%H%M%S';
		format = '.png';
	};

/**intellisense()
config = 配置
end intellisense**/

/**intellisense(config)
.X = 窗口左上角X坐标
.Y = 窗口左上角Y坐标
.Mode = 模式
.Perpage = 每页项目数量
.Theme = 主题色
.Width = 窗口宽度
.Oneclickcopy = 右键一键复制
.Wheelturn = 滚轮翻页
.CustomDir = 自定义目录列表
.CustomDir.current = 当前选择的路径
.CustomDir.root = 自定义的根目录
.CustomDir.blacklist = 黑名单
.CustomList = 自定义列表
.Clip = 剪贴板设置
.Clip.monitor = 自动监视剪贴板
.Clip.autosave = 自动保存位图
.Clip.savepath = 自动保存的路径
.Clip.filename = 自动保存的文件名
.Clip.format = 文件保存格式
end intellisense**/
